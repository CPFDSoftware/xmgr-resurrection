$!
$! configure for XMGR -- VMS version
$! Rolf Niepraschk, 12/97, niepraschk@ptb.de
$!
$ echo := WRITE SYS$OUTPUT
$!
$ CURDIR = F$ENVIRONMENT ("DEFAULT")
$ VMSDIR = F$ELEMENT (0, "]", F$ENVIRONMENT ("PROCEDURE")) + "]"
$ SET DEFAULT 'VMSDIR'
$ MAIN_DIR := [-.-]
$ CEPHES_DIR := [-.-.CEPHES]
$ SRC_DIR := [-.-.SRC]
$ GRCONVERT_DIR := [-.-.GRCONVERT]
$ EXAMPLES_DIR := [-.-.EXAMPLES]
$!
$ echo "Configuration of XMGR for VMS"
$ echo " "
$ GOSUB CHECK_OS
$ GOSUB CHECK_HW
$ GOSUB CHECK_GUI
$ GOSUB CHECK_EXT_MATH
$ echo " "
$! echo CONFIG_H_OS
$! echo CONFIG_H_HW
$! echo CONFIG_H_MATH
$! echo MAKE_OS
$! echo MAKE_HW
$! echo MAKE_GUI
$  COPY DESCRIP.MMS 'MAIN_DIR'DESCRIP.MMS
$  COPY MAKE.DEP_SRC 'SRC_DIR'MAKE.DEP
$  COPY CEPHES.MMS 'CEPHES_DIR'DESCRIP.MMS
$  COPY MAKE.DEP_CEPHES 'CEPHES_DIR'MAKE.DEP
$  COPY SRC.MMS 'SRC_DIR'DESCRIP.MMS
$  COPY GRCONVERT.MMS 'GRCONVERT_DIR'DESCRIP.MMS
$  COPY DOTEST.COM 'EXAMPLES_DIR'
$!
$ CREATE/FDL=SYS$INPUT 'MAIN_DIR'MAKE.CONF
RECORD ; FORMAT STREAM_LF
$ APPEND MAKE.CONF-COMMON, 'MAKE_OS', 'MAKE_HW', 'MAKE_GUI' -
  'MAIN_DIR'MAKE.CONF
$ CREATE/FDL=SYS$INPUT 'MAIN_DIR'CONFIG.H
RECORD ; FORMAT STREAM_LF
$ APPEND CONFIG.H-PRE, 'CONFIG_H_OS', 'CONFIG_H_HW', -
  'CONFIG_H_MATH', CONFIG.H-COMMON, CONFIG.H-POST 'MAIN_DIR'CONFIG.H
$ IF (VMS_MAJOR .LT. 7) THEN COPY PWD.H_V6 PWD.H
$!
$ SET DEFAULT 'CURDIR'
$ EXIT
$! 
$!
$CHECK_GUI:
$ @SYS$UPDATE:DECW$GET_IMAGE_VERSION SYS$SHARE:DECW$XLIBSHR.EXE DECW$VERSION
$ if f$locate("V1.1", DECW$Version) .ne. f$length(DECW$VERSION) 
$ then 
$   MOTIF := M1_1
$   echo "GUI: MOTIF 1.1"
$ ENDIF
$ if f$locate("V1.2", DECW$Version) .ne. f$length(DECW$VERSION) 
$ then 
$   MOTIF := M1_2
$   echo "GUI: MOTIF 1.2"
$ ENDIF
$ MAKE_GUI := MAKE.CONF-'MOTIF' 
$ RETURN
$!
$CHECK_HW:
$ IF F$GETSYI("ARCH_TYPE") .EQ. 1 
$ THEN  
$   HW := VAX 
$ ELSE
$   HW := AXP 
$ ENDIF
$ MAKE_HW := MAKE.CONF-'HW'
$ CONFIG_H_HW := CONFIG.H-'HW'
$ echo "Hardware: ", HW
$ RETURN
$!
$CHECK_OS:
$ VMS_MAJOR = f$extract(1,1,F$GETSYI("NODE_SWVERS",""))
$ VMS_MINOR = f$extract(3,1,F$GETSYI("NODE_SWVERS",""))
$ echo "VMS version: ''VMS_MAJOR'.''VMS_MINOR'"
$ IF VMS_MAJOR .LT. 7
$ THEN 
$   CONFIG_H_OS := CONFIG.H-VMS6
$   MAKE_OS := MAKE.CONF-VMS6
$ ELSE
$   CONFIG_H_OS := CONFIG.H-VMS7
$   MAKE_OS := MAKE.CONF-VMS7
$ ENDIF
$ RETURN
$!
$CHECK_EXT_MATH:
$ IF F$SEARCH("SYS$LIBRARY:DPML$SHR.EXE") .NES. "" 
$ THEN
$   echo "DPML: Yes"
$   CONFIG_H_MATH := CONFIG.H-DPML
$ ELSE
$   echo "DPML: No"
$   CONFIG_H_MATH := CONFIG.H-NO_DPML
$ ENDIF
$ RETURN
$!
